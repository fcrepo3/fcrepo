<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" label="Search">
		
	<mx:Script>  
		<![CDATA[
			import mx.containers.TabNavigator;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.rpc.events.FaultEvent;
			import mx.core.IFlexDisplayObject;
			import mx.managers.PopUpManager;
			import script.Alerts;
			import mx.rpc.events.ResultEvent;
            import script.ErrorHandler;
            import script.Util;
		
			[Bindable] 
			private var terms:String;
			
			private var fields:Array;		
			private var baseUrl:String;
			
			private var searchUrl:String;     			
            
            /**
             * Sets up the search tab.
             */ 
			public function setup(baseUrl:String, terms:String, fields:Array):void {
				this.baseUrl = baseUrl;
				this.terms = terms;
				this.fields = fields;
				
				runSearch();
			}
			
			/**
			 * Runs a search of the repository.
			 */
			private function runSearch():void {				
				if(terms == null || terms == "") {
					terms = "*";
				}
				this.label = "Search: " + terms;
				resultsLabel.text = "Search for: '"+terms+"'";							
				
				var fieldListing:String = "";
				for(var i:int=0; i<fields.length; i++) {
					var field:String = "&" + fields[i] + "=true"; 
					fieldListing += field;
				}
				
				searchUrl = baseUrl+"/objects?pid=true&resultFormat=xml&terms=" + 
				            Util.urlEncode(terms) + fieldListing;
				search.url = searchUrl + "&flash=true";			
				search.send();		
			}
			
			/**
			 * Handles search response
			 */
			private function searchComplete(event:ResultEvent):void {
                var error:String = ErrorHandler.getError(event.result);
                if(error != null) {
                    Alerts.showError("Could not retrieve search results" + 
                                     " due to: " + error);
                    return;
                }
			}
			
			/**
			 * Determines if there are more search results than can be displayed and
			 * provides a means to view the remaining results.
			 */
			private function checkMoreResults():void {
				var sessionToken:String = null;
				try {
					sessionToken = search.lastResult.result.listSession.token;
				} catch (err:Error) {
					more.visible = false;
				}
			}
			
			/**
			 * Retrieves the next set of search results.
			 */
			private function moreResults():void {
				var sessionToken:String = search.lastResult.result.listSession.token;
				if(sessionToken != null) {
					search.url = searchUrl + "&sessionToken=" + sessionToken;
					search.send();
					restart.visible = true;
					restart.includeInLayout = true;
				}
			}
			
			/**
			 * Restarts the search from the beginning by re-running
			 * the search.
			 */
			private function restartSearch():void {
			    search.url = searchUrl;
			    search.send()
			    restart.visible = false;
			    restart.includeInLayout = false;
			    more.visible = true;
			}
		      
		    /**
		     * Adds a tab for a selected object
		     */ 
		    private function addObjectTab(pid:String):void {
       			parentApplication.openObject(pid);
      		}
      		
      		/**
      		 * This is a hack to provide a reasonable column width when there  
      		 * are many columns while also allowing columns to be resized.
      		 * There is very likely a better way to do this.
      		 */
      		private var sizingCompete:Boolean = false;
      		private var preferredColumnWidth:int = 225;
      		private function setColumnWidth():void {
      			if(!sizingCompete && searchResults.columnCount > 4) {
	      			searchResults.width = preferredColumnWidth * searchResults.columnCount;     			      			
	      			for(var i:int=0; i<searchResults.columns.length; i++) {
	      				var column:DataGridColumn = searchResults.columns[i] as DataGridColumn;
	      				column.width=preferredColumnWidth;      				
	      			}
	      			sizingCompete = true;
      			}
      		}
		]]>
	</mx:Script>
	   
	<mx:HTTPService id="search" showBusyCursor="true" 
	                result="searchComplete(event)"
	                fault="ErrorHandler.handleFault(event)" />
    
	<mx:VBox styleName="tabContents" width="100%" height="100%">
		<mx:Label id="resultsLabel" styleName="header" />		
		
		<mx:DataGrid id="searchResults" width="95%" height="90%" minColumnWidth="4"
			dataProvider="{search.lastResult.result.resultList.objectFields}" 
			itemClick="addObjectTab(searchResults.selectedItem.pid)"
			valueCommit="checkMoreResults(); setColumnWidth();">			
		</mx:DataGrid>
		
		<mx:HBox>
		    <mx:Button id="restart" label="Back to Start" click="restartSearch()" visible="false" includeInLayout="false" />
            <mx:Button id="more" label="More Results" click="moreResults()" />
		</mx:HBox>
	</mx:VBox>
		
</mx:VBox>